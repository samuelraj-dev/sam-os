ENTRY(_start)

KERNEL_PHYS_BASE = 1M;
KERNEL_VIRT_BASE = 0xFFFFFFFF80000000;

SECTIONS
{
    . = KERNEL_VIRT_BASE + KERNEL_PHYS_BASE;
    
    _kernel_start = .;

    .text : AT(ADDR(.text) - KERNEL_VIRT_BASE) ALIGN(4K)
    {
        *(.text*)
    }

    .rodata : AT(ADDR(.rodata) - KERNEL_VIRT_BASE) ALIGN(4K)
    {
        *(.rodata*)
    }

    .data : AT(ADDR(.data) - KERNEL_VIRT_BASE) ALIGN(4K)
    {
        *(.data*)
    }

    .bss : AT(ADDR(.bss) - KERNEL_VIRT_BASE) ALIGN(4K)
    {
        *(COMMON)
        *(.bss*)
        . = ALIGN(16);
        stack_bottom = .;
        . += 0x4000;
        stack_top = .;
    }
    
    _kernel_end = .;
}